apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

def isCircle = "true".equals(System.getenv("CIRCLECI"))

ext.signing = [storeFilePath: "path/to/keystore",
               storePassword: "keystore password",
               keyAlias     : "key alias",
               keyPassword  : "key password"]

if (file('signing.gradle').exists()) {
  apply from: 'signing.gradle'
}

ext {
  okHttpVersion = '3.5.0'
  retrofitVersion = '2.1.0'
  supportLibraryVersion = '25.0.1'
}


android {
  compileSdkVersion 25
  buildToolsVersion "25.0.2"
  defaultConfig {
    applicationId "com.thoughtbot.tropos"
    minSdkVersion 19
    targetSdkVersion 25
    versionCode 6
    versionName "1.1"
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }

  signingConfigs {
    release {
      storeFile file(project.signing.storeFilePath)
      storePassword project.signing.storePassword
      keyAlias project.signing.keyAlias
      keyPassword project.signing.keyPassword
    }
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      buildConfigField("String", "FORECAST_API_KEY", "\"" + DARK_SKY_FORECAST_API_KEY + "\"")
      if (isCircle) {
        // The release build generated on CircleCI doesn't need to be signed with our real
        // keystore - we just need a release build to verify that it compiles. Using the
        // debug keystore means we don't have to expose our keystore.
      } else {
        signingConfig signingConfigs.release
      }
    }
    debug {
      buildConfigField("String", "FORECAST_API_KEY", "\"" + DARK_SKY_FORECAST_API_KEY + "\"")
    }
  }

  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
    test.java.srcDirs += 'src/test/kotlin'
  }
}


dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])

  //kotlin
  compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
  compile 'org.jetbrains.anko:anko-common:0.8.3'

  //android
  compile "com.android.support:appcompat-v7:${supportLibraryVersion}"
  compile "com.android.support:design:${supportLibraryVersion}"
  compile "com.android.support:recyclerview-v7:${supportLibraryVersion}"
  compile 'com.google.android.gms:play-services-location:10.0.0'

  //unit tests
  testCompile 'junit:junit:4.12'
  testCompile 'org.robolectric:robolectric:3.0'
  testCompile 'org.robolectric:shadows-maps:3.0'
  testCompile 'org.mockito:mockito-core:1.10.5'
  testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
  testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
  testCompile "com.nhaarman:mockito-kotlin:1.1.0"

  //automation tests
  androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
  androidTestCompile 'com.android.support.test:runner:0.5'
  androidTestCompile "com.android.support:support-annotations:${supportLibraryVersion}"

  //networking
  compile "com.squareup.retrofit2:retrofit:${retrofitVersion}"
  compile "com.squareup.retrofit2:converter-gson:${retrofitVersion}"
  compile "com.squareup.okhttp3:logging-interceptor:${okHttpVersion}"
  compile 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'

  //rx
  compile 'io.reactivex.rxjava2:rxjava:2.0.4'
  compile 'io.reactivex.rxjava2:rxandroid:2.0.1'

  //rx location
  compile 'com.patloew.rxlocation:rxlocation:1.0.1'

  //custom fonts
  compile 'uk.co.chrisjenx:calligraphy:2.1.0'
}
